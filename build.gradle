plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

group 'onethreeseven'
version '0.0.5-SNAPSHOT'

allprojects {
    repositories {
        jcenter()
        mavenCentral()
    }
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'java'
    apply plugin: 'application'
    apply plugin: 'idea'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

ext.moduleName = 'onethreeseven.common'
mainClassName = 'onethreeseven.common.Main'



compileJava {
    println classpath.asPath
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

compileTestJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'junit',
                '--add-reads', "$moduleName=junit",
                '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
        ]
        classpath = files()
    }
}

test {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'ALL-MODULE-PATH',
                '--add-reads', "$moduleName=junit",
                '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath,
        ]
        classpath = files()
    }
}

jar {
    inputs.property("moduleName", moduleName)
    manifest {
        attributes(
                'Main-Class': mainClassName,
                'Automatic-Module-Name': moduleName,
                "Implementation-Title": project.name,
                "Implementation-Version": version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'))
    }
}

jar.dependsOn(test)

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc{
    options.addStringOption('-module-path', classpath.asPath)
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from 'build/docs/javadoc'
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId project.name
            version version
            Artifact sourcesJar
            Artifact javadocJar
        }
    }
}

idea {
    module {
        inheritOutputDirs = true
        downloadJavadoc = true
        downloadSources = true
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.bintrayUser : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayKey') ? project.bintrayKey : System.getenv('BINTRAY_KEY')
    publications = ['MyPublication']
    publish = true //If version should be auto published after an upload
    pkg {
        repo = '137-common'
        name = '137-common'
        licenses = ['MIT']
        desc = 'Some common stuff I find useful in any Java project, maybe you will too?'
        websiteUrl = 'https://github.com/lukehb/137-common'
        issueTrackerUrl = 'https://github.com/lukehb/137-common/issues'
        vcsUrl = 'https://github.com/lukehb/137-common.git'
        labels = ['137', 'java', 'common', 'utilities']
        publicDownloadNumbers = true
        githubRepo = 'lukehb/137-common'
        githubReleaseNotesFile = 'README.md'
    }
}



